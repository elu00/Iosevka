$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"


glyph-module

glyph-block Letter-Latin-Eth : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	define fine : Stroke * CThin
	define coFine : mix fine Stroke 0.5

	define yMid : Ascender * 0.6
	define yMid2 : mix yMid [YSmoothMidR yMid 0 SmallArchDepthA SmallArchDepthB] 0.875

	define [ArcStartKnots] : list
		g4.right.start (Middle + CorrectionOMidS - TanSlope * (Stroke - fine)) (O + Stroke - fine + 1/16) [widths.lhs fine]
		straight.up.mid (RightSB - OX - (1 - CThin) * [HSwToV Stroke]) [YSmoothMidR yMid 0 SmallArchDepthA SmallArchDepthB]
		arcvh
		g4 (Middle - CorrectionOMidS) (yMid - O) [widths.lhs]
		archv
		g4 (SB + OX) [YSmoothMidL yMid 0 SmallArchDepthA SmallArchDepthB]
		arcvh
		g4 (Middle + CorrectionOMidS) O
		archv
		flat (RightSB - OX) [YSmoothMidR yMid 0 SmallArchDepthA SmallArchDepthB] [widths.lhs coFine]
		curl (RightSB - OX) yMid2

	create-glyph 'dInsular.curly-bar' : glyph-proc
		include : MarkSet.b
		include : dispiro
			ArcStartKnots
			quadControls 0 0.8
			g4 (SB + Stroke * 1.1) Ascender [widths.lhs]

	create-glyph 'dInsular.straight-bar' : glyph-proc
		include : MarkSet.b
		local yMockBarStart : yMid2 + Stroke * 0.3
		local xTerminal : [mix SB RightSB 0.4] + [HSwToV : 0.5 * Stroke]
		local pStraightBarStart : 0.75 - (Stroke / Ascender)
		include : intersection [MaskBelow Ascender] : dispiro
			ArcStartKnots
			flat [mix Width xTerminal pStraightBarStart] [mix yMockBarStart CAP pStraightBarStart] [widths.lhs]
			curl [mix Width xTerminal 2]                 [mix yMockBarStart CAP 2]

	select-variant 'dInsular' 0xA77A (follow -- 'eth')
	select-variant 'partial'  0x2202 (shapeFrom -- 'dInsular')

	derive-composites 'eth' 0xF0 'dInsular' : dispiro
		widths.center OverlayStroke
		flat [mix SB RightSB 0.1] [mix XH Ascender (-0.05)]
		curl [mix SB RightSB 0.95] [mix XH Ascender 0.35]

	create-glyph 'DInsular' 0xA779 : glyph-proc
		include : MarkSet.b
		local yMid : CAP * 0.65
		include : dispiro
			flat (RightSB - OX - (1 - CThin) * Stroke * HVContrast) SmallSmoothA [widths.lhs : Stroke * CThin]
			curl (RightSB - OX - (1 - CThin) * Stroke * HVContrast) (yMid - SmallSmoothB)
			arcvh
			g4 (Middle + CorrectionOMidS) (yMid - O) [widths.lhs]
			archv
			flat (SB + OX) (yMid - SmallSmoothA)
			curl (SB + OX) SmallSmoothB
			arcvh
			g4 (Middle - CorrectionOMidS) O
			archv
			flat (RightSB - OX) SmallSmoothA
			curl (RightSB - OX) (CAP - SmallSmoothB)
			hookend (CAP - O)
			g4   SB (CAP - Hook) 
